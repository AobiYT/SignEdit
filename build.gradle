apply plugin: 'java'
apply plugin: 'java-library'
apply plugin: 'application'

def name = 'SignEdit'
version '1.10.3-SNAPSHOT'

// Set to '+' to use latest Bukkit
// Otherwise, specify a version number like '1.12.2-R0.1-SNAPSHOT'
def bukkit_version = '1.15-R0.1-SNAPSHOT'

mainClassName = 'org.deltik.mc.signedit.SignEditPlugin'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
    maven {
        url "https://hub.spigotmc.org/nexus/content/repositories/snapshots/"
    }
}

configurations {
    extraLibs
}

dependencies {
    implementation group: 'org.bukkit', name: 'bukkit', version: bukkit_version
    implementation 'com.intellij:annotations:+@jar' // Workaround for org.bukkit.bukkit version 1.13.2-R0.1-SNAPSHOT regression

    implementation group: 'org.jtwig', name: 'jtwig-core', version: '5.87.0.RELEASE'
    extraLibs group: 'org.jtwig', name: 'jtwig-core', version: '5.87.0.RELEASE'

    implementation group: 'com.google.dagger', name: 'dagger', version: '2.25.4'
    extraLibs group: 'com.google.dagger', name: 'dagger', version: '2.25.4'
    annotationProcessor group: 'com.google.dagger', name: 'dagger-compiler', version: '2.25.4'
    extraLibs group: 'javax.inject', name: 'javax.inject', version: '1'

    testImplementation group: 'junit', name: 'junit', version: '4.13'
    testImplementation group: 'org.mockito', name: 'mockito-core', version: '3.2.4'
    testImplementation group: 'org.powermock', name: 'powermock-module-junit4', version: '2.0.4'
    testImplementation group: 'org.powermock', name: 'powermock-api-mockito2', version: '2.0.4'
}

sourceSets {
    main {
        java {
            srcDirs = ['src']
        }
        resources {
            srcDirs = ['src/resources']
        }
    }

    test {
        java {
            srcDir 'test'
        }
        resources {
            srcDirs = ['test/resources']
        }
    }
}

jar {
    from {
        configurations.extraLibs.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

processResources {
    with copySpec {
        from 'src/resources'
        expand(
                pluginName: name,
                pluginVersion: version,
                mainClassName: mainClassName
        )
    }
}