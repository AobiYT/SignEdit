apply plugin: 'java'
apply plugin: 'java-library'
apply plugin: 'application'
apply plugin: 'idea'

version '1.10.0-SNAPSHOT'

// Set to '+' to use latest Bukkit
// Otherwise, specify a version number like '1.12.2-R0.1-SNAPSHOT'
def bukkit_version = '1.13.2-R0.1-SNAPSHOT'

mainClassName = 'org.deltik.mc.signedit.SignEditPlugin'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
    maven {
        url "https://hub.spigotmc.org/nexus/content/repositories/snapshots/"
    }
}

configurations {
    extraLibs
}

dependencies {
    compile group: 'org.bukkit', name: 'bukkit', version: bukkit_version

    compile 'com.google.dagger:dagger:2.20'
    compile group: 'com.google.dagger', name: 'dagger', version: '2.20'
    annotationProcessor group: 'com.google.dagger', name: 'dagger-compiler', version: '2.20'

    extraLibs group: 'com.google.dagger', name: 'dagger', version: '2.20'
    extraLibs group: 'javax.inject', name: 'javax.inject', version: '1'

    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'org.mockito', name: 'mockito-core', version: '2.23.4'
    testCompile group: 'org.powermock', name: 'powermock-module-junit4', version: '1.7.4'
    testCompile group: 'org.powermock', name: 'powermock-api-mockito2', version: '1.7.4'
}

sourceSets {
    main {
        java {
            srcDirs = ['src']
        }
        resources {
            srcDirs = ['src/resources']
        }
    }

    test {
        java {
            srcDir 'test'
        }
    }
}

jar {
    from {
        configurations.extraLibs.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

processResources {
    with copySpec {
        from 'src/resources'
        expand(
            pluginVersion: version,
            mainClassName: mainClassName
        )
    }
}

idea {
    module {
        sourceDirs += files('build/classes/java/main')
        generatedSourceDirs += files('build/classes/java/main')
    }
}